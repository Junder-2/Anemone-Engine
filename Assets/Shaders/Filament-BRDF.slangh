#pragma once

#include "Common.slangh"

// ----------------------------------------------------------------
// Specular BRDF implementations.
// ----------------------------------------------------------------

float D_GGX(float roughness, float NoH, float3 halfDir)
{
    float oneMinusNoHSquared = 1.0 - NoH * NoH;
    float a = NoH * roughness;
    float k = roughness / (oneMinusNoHSquared + a * a);
    float d = k * k * (1.0 / PI);
    return d;
}

float V_SmithGGXCorrelated(float roughness, float NoV, float NoL)
{
    float a2 = roughness * roughness;
    float GGXL = NoV * sqrt((NoL - NoL * a2) * NoL + a2);
    float GGXV = NoL * sqrt((NoV - NoV * a2) * NoV + a2);
    return 0.5 / (GGXV + GGXL);
}

// Saves 2 sqrts but is mathematically wrong.
float V_SmithGGXCorrelatedFast(float roughness, float NoV, float NoL)
{
    return 0.5 / lerp(2 * NoL * NoV, NoL + NoV, roughness);
}

float3 F_Schlick(float3 f0, float f90, float VoH)
{
    return f0 + (f90 - f0) * pow(1 - VoH, 5);
}

float3 F_Schlick(float3 f0, float VoH)
{
    float f = pow(1.0 - VoH, 5.0);
    return f + f0 * (1 - f);
}

float F_Schlick(float f0, float f90, float VoH)
{
    return f0 + (f90 - f0) * pow(1 - VoH, 5);
}

// ----------------------------------------------------------------
// Specular BRDF dispatch.
// ----------------------------------------------------------------

float Distribution(float roughness, float NoH, float3 halfDir)
{
    return D_GGX(roughness, NoH, halfDir);
}

float Visibility(float roughness, float NoV, float NoL)
{
    return V_SmithGGXCorrelated(roughness, NoV, NoL);
}

float3 Fresnel(float3 f0, float LoH)
{
    float f90 = saturate(dot(f0, float3(50.0 * 0.33)));
    return F_Schlick(f0, f90, LoH);
}

// ----------------------------------------------------------------
// Diffuse BRDF implementations.
// ----------------------------------------------------------------

// Filament (Lambertian)
float Fd_Lambert()
{
    return 1.0 / PI;
}

// Disney (Burley)
float Fd_Burley(float roughness, float NoV, float NoL, float LoH)
{
    float f90 = 0.5 + 2.0 * roughness * LoH * LoH;
    float lightScatter = F_Schlick(1.0, f90, NoL);
    float viewScatter = F_Schlick(1.0, f90, NoV);
    return lightScatter * viewScatter / PI;
}

// ----------------------------------------------------------------
// Diffuse BRDF dispatch.
// ----------------------------------------------------------------

float Diffuse(float roughness, float NoV, float NoL, float LoH)
{
    return Fd_Burley(roughness, NoV, NoL, LoH);
}